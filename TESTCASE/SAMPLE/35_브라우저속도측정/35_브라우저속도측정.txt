;-----------------------------------------------------------------------------------------------------------------------------------------
;# 설명 : 4개 URL로 각각 3번씩 접속하여 페이지 로딩시간을 파일로 기록한다.
;# IE는 브라우저 DocumentComplete 이벤트 발생시간을, 그외의 브라우저는 브라우저 상단 로딩완료 이미지 변경시간을 기준으로 함.
;-----------------------------------------------------------------------------------------------------------------------------------------

;데이터 결과 저장 파일명
"$테스트결과파일=|$GUITAR_리포트경로|\testresult.csv" 설정 한다.
"$파일핸들=FileOpen('|$테스트결과파일|',512 + 2)" 계산한다.

IE 생성한다.

; 4개 URL에 대해 브라우저 로딩 테스트 수행
$i=1 설정 $i=4 반복 {

	$i=1 같으면 $측정URL=http://www.naver.com 설정한다.
	$i=2 같으면 $측정URL=http://www.daum.net  설정한다.
	$i=3 같으면 $측정URL=http://map.naver.com  설정한다.
	$i=4 같으면 $측정URL=http://map.daum.net 설정한다.

	; 각 URL 별로 3회씩 반복
	$j=1 설정 $j=3 반복 {

		$측정URL 접속한다.

		; 저장 내용
		"$저장내용=|$측정URL|,|$j|,|$GUITAR_현재날짜와시간|,|$GUITAR_최근접속소요시간|" 설정한다. 

		; 파일에 Line 출력
		"$저장결과=FileWriteLine(|$파일핸들|,'|$저장내용|')" 계산한다.
	}
}
"$파일닫기=FileClose(|$파일핸들|)" 계산한다.


IE 종료한다.

; 최종 생성된 csv 파일을 테스트 종료후 자동으로 열게 함
"$실행결과=ShellExecute('|$테스트결과파일|')" 계산한다.

